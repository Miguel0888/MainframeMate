plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'application'
    id 'antlr'
}

repositories {
    mavenCentral()
}

//def javaToolsJar = file("${System.getenv('JAVA_HOME')}/lib/tools.jar")
def javaHome = System.properties['java.home']
def javaToolsJar = file("$javaHome/../lib/tools.jar").canonicalFile
if (!javaToolsJar.exists()) {
    throw new GradleException("tools.jar nicht gefunden! Stelle sicher, dass JAVA_HOME auf ein Java 8 JDK zeigt.")
}

dependencies {
    implementation project(':core')

    // FTP
    implementation 'commons-net:commons-net:3.9.0'

    // NDV (Natural Development Server) - nur noch Laufzeit-Abhängigkeit (wird per NdvProxyBridge geladen)
    // Das JAR wird NICHT mehr in den Classpath gepackt; NdvClient kompiliert gegen Stubs im ndv-Modul.
    implementation project(':ndv')

    // JSON
    implementation 'com.google.code.gson:gson:2.10.1'

    // Windows Key Store
    implementation 'net.java.dev.jna:jna:5.13.0'
    implementation 'net.java.dev.jna:jna-platform:5.13.0'

    // Excel
    implementation 'org.apache.poi:poi:5.2.3'
    implementation 'org.apache.poi:poi-ooxml:5.2.3'

    // GUI (RSyntaxTextArea)
    implementation 'com.fifesoft:rsyntaxtextarea:3.3.3'

    // Pretty Print
    implementation 'com.google.googlejavaformat:google-java-format:1.7'

    // REST (AI)
    implementation 'com.squareup.okhttp3:okhttp:4.9.3'

    // tools.jar für JavaCompiler (nur Java 8, ab Java 9 JShell verwenden!)
    implementation files(javaToolsJar)

    // Testing
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'

    // Eigene Plugins lokal zur Laufzeit verfügbar machen
    runtimeOnly project(':plugins:excelimport') // weitere Plugins kannst du hier ergänzen
    runtimeOnly project(':plugins:webSearch')

    // VFS (local)
    implementation 'org.apache.commons:commons-vfs2:2.10.0'

    // Document Ingestion - Tika (Type Detection + Fallback Extraction)
    implementation 'org.apache.tika:tika-core:2.9.1'
    implementation('org.apache.tika:tika-parsers-standard-package:2.9.1') {
        // Exclude flexmark (requires Java 11+)
        exclude group: 'com.vladsch.flexmark'
    }

    // PDFBox for PDF extraction
    implementation 'org.apache.pdfbox:pdfbox:2.0.30'

    // jsoup for HTML parsing
    implementation 'org.jsoup:jsoup:1.17.2'

    // Markdown rendering (commonmark - Java 8 compatible)
    implementation 'org.commonmark:commonmark:0.21.0'
    implementation 'org.commonmark:commonmark-ext-gfm-tables:0.21.0'

    // Lucene for lexical search (RAG) - 8.11.x is Java 8 compatible
    implementation 'org.apache.lucene:lucene-core:8.11.3'
    implementation 'org.apache.lucene:lucene-queryparser:8.11.3'
    implementation 'org.apache.lucene:lucene-analyzers-common:8.11.3'

    // ANTLR for JCL parsing (4.9.3 = last Java 8 compatible version)
    antlr 'org.antlr:antlr4:4.9.3'
    implementation 'org.antlr:antlr4-runtime:4.9.3'

    // ICU4J Charset Provider (für IBM Codepages / CharsetICU). Muss im App-Classpath liegen,
    // weil java.nio.charset.Charset die Provider über den (System/App-)ClassLoader cached.
    runtimeOnly files("$rootDir/lib/icu4j-4_8_1.jar")
    runtimeOnly files("$rootDir/lib/icu4j-charset-4_8_1.jar")
}

shadowJar {
    mergeServiceFiles()
}

application {
    mainClass = 'de.bund.zrb.Main'
}

test {
    useJUnitPlatform()
}

// ANTLR configuration
generateGrammarSource {
    arguments += ['-visitor', '-listener', '-package', 'de.bund.zrb.jcl.parser']
    outputDirectory = file("${project.buildDir}/generated-src/antlr/main/de/bund/zrb/jcl/parser")
}

sourceSets {
    main {
        java {
            srcDir "${project.buildDir}/generated-src/antlr/main"
        }
    }
}

compileJava.dependsOn generateGrammarSource

// Ensure all plugins are built and installed when assembling the app
tasks.named('assemble') {
    dependsOn ':plugins:excelimport:installPlugin'
    dependsOn ':plugins:webSearch:installPlugin'
}

//// Includes the AI driver and license files into the JAR
//sourceSets {
//    main {
//        resources {
//            srcDir 'src/main/resources'
//            include '**/*.exe'
//            include '**/*.txt'
//        }
//    }
//}
